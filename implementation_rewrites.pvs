implementation_rewrites_1: THEORY
BEGIN

IMPORTING implementation_trans_1

q: VAR state_I

Rewrite_dec_ex: LEMMA impl_dec_ex(I_step(q)) = impl_dec_id(q)
Rewrite_dec_mem: LEMMA impl_dec_mem(I_step(q)) = impl_dec_ex(q)
Rewrite_dec_wb: LEMMA impl_dec_wb(I_step(q)) = impl_dec_mem(q)

Rewrite_dst_ex: LEMMA impl_ex_dst(I_step(q)) = impl_id_dst(q)
Rewrite_dst_mem: LEMMA impl_mem_dst(I_step(q)) = impl_ex_dst(q)
Rewrite_dst_wb: LEMMA impl_wb_dst(I_step(q)) = impl_mem_dst(q)

Rewrite_instr_ex: LEMMA (I_step(q))`ex_s`instr = q`id_s`instr 
Rewrite_instr_mem: LEMMA (I_step(q))`mem_s`instr = q`ex_s`instr 
Rewrite_instr_wb: LEMMA (I_step(q))`wb_s`instr = q`mem_s`instr 

Rewrite_pc_ex: LEMMA (I_step(q))`ex_s`pc = q`id_s`pc 
Rewrite_pc_mem: LEMMA (I_step(q))`mem_s`pc = q`ex_s`pc 

Rewrite_state_ex: LEMMA I_step(q)`ex_s`state = i1s_next_ex_state(q)
Rewrite_state_mem: LEMMA I_step(q)`mem_s`state = i1s_next_mem_state(q)

AUTO_REWRITE- Rewrite_state_ex
AUTO_REWRITE- Rewrite_state_mem

%|- Rewrite_dec_* : PROOF
%|- Rewrite_dst_* : PROOF
%|- Rewrite_instr* : PROOF
%|- Rewrite_pc*: PROOF
%|- Rewrite_state*: PROOF
%|- (then (skosimp) (auto-rewrite-theory implementation_rewrites_1) (impl-defs) (assert) (expand I_step) (assert))
%|- QED

Rewrite_sync_ex_rs_1: LEMMA syn1_ex_need_rs_ex(I_step(q)) IMPLIES syn1_id_need_rs_ex(q)
Rewrite_sync_ex_rt_1: LEMMA syn1_ex_need_rt_ex(I_step(q)) IMPLIES syn1_id_need_rt_ex(q)
Rewrite_sync_ex_rs_2: LEMMA syn1_ex_need_rs_ex(I_step(q)) IMPLIES NOT syn1_stall_issue(q)
Rewrite_sync_ex_rt_2: LEMMA syn1_ex_need_rt_ex(I_step(q)) IMPLIES NOT syn1_stall_issue(q)


Rewrite_sync_mem_rt_1: LEMMA syn1_mem_need_rt_mem(I_step(q)) IMPLIES syn1_ex_need_rt_mem(q)
Rewrite_sync_mem_rt_2: LEMMA syn1_ex_need_rt_mem(I_step(q)) IMPLIES syn1_id_need_rt_mem(q)



Rewrite_sync_wb_wb: LEMMA syn1_get_wb_wb?(I_step(q)) = syn1_get_mem_wb?(q)

Rewrite_sync_mem_wb: LEMMA syn1_get_mem_wb?(I_step(q)) IMPLIES syn1_get_ex_wb?(q)


%|- Rewrite_sync_* : PROOF
%|- (then (skosimp) (auto-rewrite-theory implementation_rewrites_1)
%|-  (impl-defs) (assert) (expand I_step) (assert) (flatten) (assert)
%|-  (case-analysis))
%|- QED


Rewrite_ex_need_rs_1: LEMMA q`ex_s`state = normal AND impl_dec_ex(q)`store? IMPLIES syn1_ex_need_rs_ex(q)
Rewrite_ex_need_rs_2: LEMMA q`ex_s`state = normal AND impl_dec_ex(q)`load? IMPLIES syn1_ex_need_rs_ex(q)
Rewrite_ex_selb_not_rt_1: LEMMA impl_dec_ex(q)`store? IMPLIES impl_dec_ex(q)`selb /= rt
Rewrite_ex_selb_not_rt_2: LEMMA impl_dec_ex(q)`load? IMPLIES impl_dec_ex(q)`selb /= rt
Rewrite_ex_store_not_ov: LEMMA impl_dec_ex(q)`store? IMPLIES NOT impl_dec_ex(q)`checkov?
Rewrite_ex_load_not_ov: LEMMA impl_dec_ex(q)`load? IMPLIES NOT impl_dec_ex(q)`checkov?
Rewrite_ex_checkov_aluout: LEMMA impl_dec_ex(q)`checkov? IMPLIES impl_dec_ex(q)`wbsrc = aluout
Rewrite_ex_checkov_wb:     LEMMA impl_dec_ex(q)`checkov? IMPLIES impl_dec_ex(q)`wb?
Rewrite_ex_store_not_wb: LEMMA impl_dec_ex(q)`store? IMPLIES NOT impl_dec_ex(q)`wb?
Rewrite_ex_load_wbsrc: LEMMA load?(impl_dec_ex(q)`wbsrc) =  impl_dec_ex(q)`load?
Rewrite_id_store_not_invalid: LEMMA impl_dec_id(q)`store? IMPLIES NOT impl_dec_id(q)`invalid?
Rewrite_id_wb_not_invalid: LEMMA impl_dec_id(q)`wb? IMPLIES NOT impl_dec_id(q)`invalid?
Rewrite_id_beq_not_invalid: LEMMA impl_dec_id(q)`beq? IMPLIES NOT impl_dec_id(q)`invalid?
Rewrite_id_bne_not_invalid: LEMMA impl_dec_id(q)`bne? IMPLIES NOT impl_dec_id(q)`invalid?
Rewrite_id_jr_not_invalid: LEMMA impl_dec_id(q)`jr? IMPLIES NOT impl_dec_id(q)`invalid?
Rewrite_id_jmp_not_invalid: LEMMA impl_dec_id(q)`jmp? IMPLIES NOT impl_dec_id(q)`invalid?
Rewrite_id_load_not_invalid: LEMMA impl_dec_id(q)`load? IMPLIES NOT impl_dec_id(q)`invalid?
Rewrite_id_checkov_not_invalid: LEMMA impl_dec_id(q)`checkov? IMPLIES NOT impl_dec_id(q)`invalid?

Rewrite_id_beq_not_checkov: LEMMA impl_dec_id(q)`beq? IMPLIES NOT impl_dec_id(q)`checkov?
Rewrite_id_bne_not_checkov: LEMMA impl_dec_id(q)`bne? IMPLIES NOT impl_dec_id(q)`checkov?
Rewrite_id_jr_not_checkov: LEMMA impl_dec_id(q)`jr? IMPLIES NOT impl_dec_id(q)`checkov?
Rewrite_id_jmp_not_checkov: LEMMA impl_dec_id(q)`jmp? IMPLIES NOT impl_dec_id(q)`checkov?

Rewrite_id_beq_not_load: LEMMA impl_dec_id(q)`beq? IMPLIES NOT impl_dec_id(q)`load?
Rewrite_id_bne_not_load: LEMMA impl_dec_id(q)`bne? IMPLIES NOT impl_dec_id(q)`load?
Rewrite_id_jr_not_load: LEMMA impl_dec_id(q)`jr? IMPLIES NOT impl_dec_id(q)`load?
Rewrite_id_jmp_not_load: LEMMA impl_dec_id(q)`jmp? IMPLIES NOT impl_dec_id(q)`load?

Rewrite_id_beq_not_store: LEMMA impl_dec_id(q)`beq? IMPLIES NOT impl_dec_id(q)`store?
Rewrite_id_bne_not_store: LEMMA impl_dec_id(q)`bne? IMPLIES NOT impl_dec_id(q)`store?
Rewrite_id_jr_not_store: LEMMA impl_dec_id(q)`jr? IMPLIES NOT impl_dec_id(q)`store?
Rewrite_id_jmp_not_store: LEMMA impl_dec_id(q)`jmp? IMPLIES NOT impl_dec_id(q)`store?

Rewrite_id_beq_not_bne: LEMMA impl_dec_id(q)`beq? IMPLIES NOT impl_dec_id(q)`bne?
Rewrite_id_beq_not_jmp: LEMMA impl_dec_id(q)`beq? IMPLIES NOT impl_dec_id(q)`jmp?
Rewrite_id_beq_not_jr: LEMMA impl_dec_id(q)`beq? IMPLIES NOT impl_dec_id(q)`jr?
Rewrite_id_bne_not_jmp: LEMMA impl_dec_id(q)`bne? IMPLIES NOT impl_dec_id(q)`jmp?
Rewrite_id_bne_not_jr: LEMMA impl_dec_id(q)`bne? IMPLIES NOT impl_dec_id(q)`jr?


%|- Rewrite_ex_* : PROOF
%|- Rewrite_id_* : PROOF
%|- (then (skosimp) (impl-defs) (assert)
%|-  (auto-rewrite-theory decoder_props) (assert))
%|- QED

%Rewrite_state_ex_mem: LEMMA I_step(q)`mem_s`state = normal IMPLIES q`ex_s`state = normal

%|- Rewrite_state_ex_mem : PROOF
%|- (then (skosimp) (impl-defs) (assert) (expand I_step) (assert)
%|-  (case-analysis))
%|- QED

Rewrite_ex_alu_wb_1: LEMMA q`ex_s`state = normal AND impl_dec_ex(q)`wb? AND impl_dec_ex(q)`wbsrc = aluout IMPLIES syn1_ex_need_rs_ex(q) OR syn1_ex_need_rt_ex(q)
Rewrite_ex_alu_wb_2: LEMMA q`ex_s`state = normal AND impl_dec_ex(q)`wb? AND impl_dec_ex(q)`wbsrc = aluout IMPLIES syn1_ex_need_rt_ex(q) OR impl_dec_ex(q)`selb /= rt

%|- Rewrite_ex_alu_wb_* : PROOF
%|- (then (skosimp) (impl-defs) (assert)
%|-  (auto-rewrite-theory decoder_props) (assert) (case-analysis))
%|- QED

% 有没有更好的办法避免手动重写？拆成标准形式！！
Rewrite_syn1_id_1: LEMMA q`id_s`state = normal AND  impl_dec_id(q)`beq?  IMPLIES syn1_id_need_rs_id(q)

Rewrite_syn1_id_2: LEMMA q`id_s`state = normal AND  impl_dec_id(q)`bne?  IMPLIES syn1_id_need_rs_id(q)

Rewrite_syn1_id_3: LEMMA q`id_s`state = normal AND  impl_dec_id(q)`jr?  IMPLIES syn1_id_need_rs_id(q)

Rewrite_syn1_id_4: LEMMA q`id_s`state = normal AND  impl_dec_id(q)`beq?  IMPLIES syn1_id_need_rt_id(q)

Rewrite_syn1_id_5: LEMMA q`id_s`state = normal AND  impl_dec_id(q)`bne?  IMPLIES syn1_id_need_rt_id(q)


END implementation_rewrites_1
