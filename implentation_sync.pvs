implementation_state_1_sync: THEORY
BEGIN

IMPORTING implementation_state_1

q: VAR state_I

% 译码信号

% T_need = 0
syn1_id_need_rs(q): bool = LET dec = impl_dec_id(q) IN q`id_s`state = normal AND (dec`beq? OR dec`bne? OR dec`jr? ) AND rs(q`id_s`instr) /= zero_reg

syn1_id_need_rt(q): bool = LET dec = impl_dec_id(q) IN q`id_s`state = normal AND (dec`beq? OR dec`bne? ) AND rt(q`id_s`instr) /= zero_reg

% T_need  = 1
syn1_future_ex_need_rs(q): bool = LET dec=impl_dec_id(q) IN 
                           q`id_s`state = normal AND rs(q`id_s`instr) /= zero_reg AND NOT dec`beq? AND NOT dec`bne? AND NOT dec`jr? AND dec`sela = rs 

syn1_future_ex_need_rt(q): bool = LET dec=impl_dec_id(q) IN 
                           q`id_s`state = normal AND rt(q`id_s`instr) /= zero_reg AND NOT dec`beq? AND NOT dec`bne? AND (dec`sela = rt OR ( dec`selb = rt AND NOT dec`store?) ) 
                                    
% T_need = 2
syn1_future_mem_need_rt(q): bool = LET dec=impl_dec_id(q) IN 
                           q`id_s`state = normal AND rs(q`id_s`instr) /= zero_reg AND dec`store? 

syn1_get_ex_dst(q): bvec[5] = LET dec=impl_dec_ex(q), instr=q`ex_s`instr IN
         TABLE
          dec`seld
          %--------+-----------++
          | rd     | rd(instr) ||
          | rt     | rt(instr) ||
          | ra     | ra_reg    ||
          %--------+-----------++
         ENDTABLE

syn1_get_mem_wb?(q): bool = q`mem_s`state = normal AND impl_dec_mem(q)`wb?

syn1_get_wb_wb?(q):  bool = NOT q`wb_s`bubble AND impl_dec_wb(q)`wb?

syn1_get_ex_wb?(q):  bool = q`ex_s`state = normal AND impl_dec_ex(q)`wb?

syn1_get_mem_dst(q): bvec[5] = LET dec=impl_dec_mem(q), instr=q`mem_s`instr IN
         TABLE
          dec`seld
          %--------+-----------++
          | rd     | rd(instr) ||
          | rt     | rt(instr) ||
          | ra     | ra_reg    ||
          %--------+-----------++
         ENDTABLE




% 结果是否产生
% ex 阶段永远不产生结果
% mem阶段可能有结果
% wb阶段永远有结果
syn1_current_mem_has_wb_val(q): bool = q`mem_s`state = normal AND NOT impl_dec_mem(q)`load?

syn1_next_mem_has_wb_val(q): bool = q`ex_s`state = normal AND NOT impl_dec_ex(q)`load?


% 匹配关系


%% rs
% id <-> ex 阶段永远不可能转发，
syn1_rs_id_indep_ex(q): bool = LET dec = impl_dec_ex(q) IN 
                        syn1_id_need_rs(q) IMPLIES 
                             ( syn1_get_ex_wb?(q) IMPLIES rs(q`id_s`instr) /= syn1_get_ex_dst(q)) 

% id <-> mem 阶段，可能转发，也可能不需要转发

syn1_rs_id_indep_mem(q): bool = syn1_id_need_rs(q) IMPLIES 
                               ( syn1_get_mem_wb?(q) IMPLIES rs(q`id_s`instr) /= syn1_get_mem_dst(q) )

syn1_rs_id_fwd_match_mem(q): bool = syn1_id_need_rs(q) AND 
                               ( syn1_get_mem_wb?(q) AND rs(q`id_s`instr) = syn1_get_mem_dst(q) AND syn1_current_mem_has_wb_val(q) )



syn1_rs_ex_indep_mem(q): bool = LET dec=impl_dec_ex(q) IN 
                             syn1_future_ex_need_rs(q) IMPLIES  
                             ( syn1_get_mem_wb?(q) IMPLIES rs(q`id_s`instr) /=  syn1_get_ex_dst(q) )

syn1_rs_ex_fwd_match_mem(q): bool = LET dec=impl_dec_ex(q) IN 
                             syn1_future_ex_need_rs(q) AND 
                              syn1_get_ex_wb?(q) AND rs(q`id_s`instr) =  syn1_get_ex_dst(q) AND  syn1_next_mem_has_wb_val(q)


syn1_rs_may_fwd_match(q): bool = syn1_rs_id_indep_ex(q) AND ( syn1_rs_id_indep_mem(q) OR syn1_rs_id_fwd_match_mem(q)) AND (syn1_rs_ex_indep_mem(q) OR syn1_rs_ex_fwd_match_mem(q))


% id <-> ex 阶段永远不可能转发，
syn1_rt_id_indep_ex(q): bool = LET dec = impl_dec_ex(q) IN 
                        syn1_id_need_rt(q) IMPLIES 
                             ( syn1_get_ex_wb?(q) IMPLIES rt(q`id_s`instr) /= syn1_get_ex_dst(q)) 

% id <-> mem 阶段，可能转发，也可能不需要转发

syn1_rt_id_indep_mem(q): bool = syn1_id_need_rt(q) IMPLIES 
                               (syn1_get_mem_wb?(q) IMPLIES rt(q`id_s`instr) /= syn1_get_mem_dst(q) )

syn1_rt_id_fwd_match_mem(q): bool = syn1_id_need_rt(q) AND 
                               (syn1_get_mem_wb?(q) AND rt(q`id_s`instr) = syn1_get_mem_dst(q) AND syn1_current_mem_has_wb_val(q) )



syn1_rt_ex_indep_mem(q): bool = LET dec=impl_dec_ex(q) IN 
                             syn1_future_ex_need_rt(q) IMPLIES  
                             ( syn1_get_ex_wb?(q) IMPLIES rt(q`id_s`instr) /=  syn1_get_ex_dst(q) )

syn1_rt_ex_fwd_match_mem(q): bool = LET dec=impl_dec_ex(q) IN 
                             syn1_future_ex_need_rt(q) AND 
                             syn1_get_ex_wb?(q) AND rt(q`id_s`instr) =  syn1_get_ex_dst(q) AND  syn1_next_mem_has_wb_val(q)


% 由于wb阶段永远有结果，mem阶段的需求永远可以获得转发，其实可以省略?? 
% lw $1, 4($2); sw $1, 8($2) ==> sw 到 mem阶段直接转发
% lw $1, $2(4); addu $4, $6, $5; sw $1, $3(8) ==> sw 到 mem阶段无法从寄存器读，也无法转发，但可以在ex阶段转发！
% lw $1, $2(4); addu $3, $4, $5; sw $3, 8($1) ==>  

syn1_rt_may_fwd_match(q): bool = syn1_rt_id_indep_ex(q) AND ( syn1_rt_id_indep_mem(q) OR syn1_rt_id_fwd_match_mem(q)) AND (syn1_rt_ex_indep_mem(q) OR syn1_rt_ex_fwd_match_mem(q))



% 暂停信号生成
syn1_stall_issue(q): bool = NOT ( syn1_rs_may_fwd_match(q) AND syn1_rt_may_fwd_match(q) )


% 转发器信号




syn1_mux_id_rs(q): bvec[32] = IF rs(q`id_s`instr) = zero_reg THEN zero
              ELSIF q`mem_s`state = normal AND syn1_get_mem_wb?(q) AND rs(q`id_s`instr) = syn1_get_mem_dst(q)  THEN q`mem_s`aluout 
              ELSIF syn1_get_wb_wb?(q) AND rs(q`id_s`instr) = impl_wb_dst(q)  THEN q`wb_s`val
              ELSE  impl_rf_output(q)`val_1
              ENDIF

syn1_mux_id_rt(q): bvec[32] = IF rt(q`id_s`instr) = zero_reg THEN zero
              ELSIF q`mem_s`state = normal AND syn1_get_mem_wb?(q) AND rt(q`id_s`instr) = syn1_get_mem_dst(q)  THEN q`mem_s`aluout 
              ELSIF syn1_get_wb_wb?(q) AND rt(q`id_s`instr) = impl_wb_dst(q)  THEN q`wb_s`val
              ELSE  impl_rf_output(q)`val_2
              ENDIF

% 
syn1_mux_ex_rs(q): bvec[32] = 
              IF    rs(q`ex_s`instr) /= zero_reg AND q`mem_s`state = normal AND syn1_get_mem_wb?(q) AND rs(q`ex_s`instr) = syn1_get_mem_dst(q) THEN q`mem_s`aluout
              ELSIF rs(q`ex_s`instr) /= zero_reg AND syn1_get_wb_wb?(q) AND rs(q`ex_s`instr) = impl_wb_dst(q) THEN q`wb_s`val
              ELSE q`ex_s`rs
              ENDIF

syn1_mux_ex_rt(q): bvec[32] = 
              IF    rt(q`ex_s`instr) /= zero_reg AND q`mem_s`state = normal AND syn1_get_mem_wb?(q) AND rt(q`ex_s`instr) = syn1_get_mem_dst(q) THEN q`mem_s`aluout
              ELSIF rt(q`ex_s`instr) /= zero_reg AND syn1_get_wb_wb?(q) AND rt(q`ex_s`instr) = impl_wb_dst(q) THEN q`wb_s`val
              ELSE q`ex_s`rt
              ENDIF

syn1_mux_mem_rt(q): bvec[32] = 
              IF  rt(q`mem_s`instr) /= zero_reg AND syn1_get_wb_wb?(q) AND rt(q`mem_s`instr) = impl_wb_dst(q) THEN q`wb_s`val
              ELSE q`mem_s`rt
              ENDIF



END implementation_state_1_sync
